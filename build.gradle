import org.jetbrains.grammarkit.tasks.GenerateLexer
import org.jetbrains.grammarkit.tasks.GenerateParser

buildscript {
    repositories {
        maven { url 'http://dl.bintray.com/jetbrains/intellij-plugin-service' }
        maven { url 'https://jitpack.io' }
    }
}

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'org.jetbrains.intellij' version '0.4.21'
    id 'com.palantir.git-version' version '0.11.0'
    id 'org.jetbrains.grammarkit' version '2020.2.1'
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.jetbrains.kotlin:kotlin-reflect:1.3.72'
    // explicit dependency to avoid version conflicts

    testCompile 'io.kotlintest:kotlintest:2.0.7'
}

group 'com.github.tomas-milata'
version gitVersion() // uses git describe

sourceCompatibility = 1.8
targetCompatibility = 1.8
apply plugin: 'org.jetbrains.grammarkit'

def genRoot = file('src/generated')

sourceSets {
    main {
        java.srcDirs 'src/main', genRoot
        resources.srcDir 'resources'
    }
}

intellij {
    version '2020.2.3' // IntelliJ Platform IDE version used to build the plugin
    plugins = ['java', 'org.intellij.scala:2020.2.17']
}

patchPluginXml {
    // sinceBuild defined by `intellij.version`

    /** should be e.g. 203.* if sinceBuild is 202 (or 2020.2)
     * to give compatibility with a next minor a shot */
    untilBuild = '203.*'

    changeNotes  {
        """
        <h1>2020.2.4</h1>
        <ul>
            <li>Added code completion an "go to definition" for other routers</li>
            <li>Improved controller code completion performance</li>
        </ul>
        """
    }
}

publishPlugin {
    token System.getenv('INTELLIJ_PUBLISH_TOKEN')
    channels { publishChannel }
}

task generateParser(type: GenerateParser) {
    source = 'src/main/grammar/Routes.bnf'
    pathToParser = '/com/github/tomasmilata/intelliroutes/parser/RoutesParser.java'
    pathToPsiRoot = '/com/github/tomasmilata/intelliroutes/psi'
    targetRoot = genRoot
    purgeOldFiles = true
}

task generateLexer(type: GenerateLexer) {
    dependsOn generateParser
    source = 'src/main/jflex/Routes.flex'
    targetDir = "${genRoot}/com/github/tomasmilata/intelliroutes"
    targetClass = 'RoutesLexer'
    purgeOldFiles = true
}

compileKotlin {
    dependsOn generateLexer
}