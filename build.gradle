buildscript {
    repositories {
        maven { url 'https://dl.bintray.com/jetbrains/intellij-plugin-service' }
        maven { url 'https://jitpack.io' }
        mavenCentral()
    }
}

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.6.21'
    id 'org.jetbrains.intellij' version '1.6.0'
    id 'com.palantir.git-version' version '0.15.0'
    id 'org.jetbrains.grammarkit' version '2021.2.2'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-reflect:1.6.21'
    // explicit dependency to avoid version conflicts

    testImplementation 'io.kotlintest:kotlintest:2.0.7'
}

group 'com.github.tomas-milata'
version gitVersion() // uses git describe

//apply plugin: 'org.jetbrains.grammarkit'

def genRoot = 'src/generated'

sourceSets {
    main {
        java.srcDirs 'src/main', genRoot
        resources.srcDir 'resources'
    }
}

intellij {
    version = '2022.1' // IntelliJ Platform IDE version used to build the plugin
    plugins = ['com.intellij.java', 'org.intellij.scala:2022.1.14']
}

patchPluginXml {
    // sinceBuild defined by `intellij.version`

    /** should be e.g. 203.* if sinceBuild is 202 (or 2020.2)
     * to give compatibility with a next minor a shot */
    untilBuild = '231.*'

    changeNotes =
        """
        <h1>2020.2.4</h1>
        <ul>
            <li>Added code completion and "go to definition" for other routers</li>
            <li>Improved controller code completion performance</li>
            <li>Added icons next to code completion suggestions</li>
        </ul>
        """
}

publishPlugin {
    token = System.getenv('INTELLIJ_PUBLISH_TOKEN')
//    channels = [publishChannel]
}

generateParser {
    source = 'src/main/grammar/Routes.bnf'
    pathToParser = '/com/github/tomasmilata/intelliroutes/parser/RoutesParser.java'
    pathToPsiRoot = '/com/github/tomasmilata/intelliroutes/psi'
    targetRoot = genRoot
    purgeOldFiles = true
}

generateLexer {
    dependsOn generateParser
    source = 'src/main/jflex/Routes.flex'
    targetDir = "${genRoot}/com/github/tomasmilata/intelliroutes"
    targetClass = 'RoutesLexer'
    purgeOldFiles = true
}

compileKotlin {
    dependsOn generateLexer
}