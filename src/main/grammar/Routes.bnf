{
  parserClass="com.github.tomasmilata.intelliroutes.parser.RoutesParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Routes"
  psiImplClassSuffix="Impl"
  psiPackage="com.github.tomasmilata.intelliroutes.psi"
  psiImplPackage="com.github.tomasmilata.intelliroutes.psi.impl"

  elementTypeHolderClass="com.github.tomasmilata.intelliroutes.psi.RoutesTypes"
  elementTypeClass="com.github.tomasmilata.intelliroutes.psi.RoutesElementType"
  tokenTypeClass="com.github.tomasmilata.intelliroutes.psi.RoutesTokenType"
}

routesFile ::= item_*

private item_ ::= (route|redirect|COMMENT|EOL)

route ::= modifiers? VERB path call
modifiers ::= PLUS MODIFIER* EOL
path ::= SLASH ( pathSegment (SLASH pathSegment)* SLASH?)? WILDCARD_PARAMETER?
pathSegment ::= STATIC_PATH_SEGMENT | PATH_PARAMETER | (PATH_REGEX_PARAM(STATIC_PATH_SEGMENT)?)+
call ::= CONTROLLER_METHOD ( OPENING_PARENTHESIS arguments? CLOSING_PARENTHESIS)?
arguments ::= argument (COMMA argument)*
argument ::= ARGUMENT_NAME (COLON ARGUMENT_TYPE)? (ARGUMENT_EQUAL ARGUMENT_VALUE)?
redirect ::= ARROW path ROUTER_REFERENCE
